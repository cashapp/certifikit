buildscript {
  ext.versions = [
      'assertj': '3.15.0',
      'java': '1.8',
      'junit': '4.13',
      'kotlin': '1.3.72',
      'ktlint': '0.36.0',
      'okio': '2.7.0',
  ]

  ext.deps = [
      'assertj': "org.assertj:assertj-core:${versions.assertj}",
      'junit': "junit:junit:${versions.junit}",
      'kotlinStdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
      'okio': "com.squareup.okio:okio:${versions.okio}",
  ]

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.1"
    classpath 'com.diffplug.spotless:spotless-plugin-gradle:3.27.1'
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
}

allprojects {
  group = 'app.cash.certifikit'
  version = '0.1.0-SNAPSHOT'

  repositories {
    mavenCentral()
    maven {
      url 'https://dl.bintray.com/kotlin/dokka'
    }
    google()
  }
}

/** Configure building for Java+Kotlin projects. */
subprojects { project ->
  apply plugin: 'java-library'
  apply plugin: 'org.jetbrains.kotlin.platform.jvm'
  apply plugin: 'org.jetbrains.dokka'
  apply plugin: 'com.diffplug.gradle.spotless'
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8

  spotless {
    kotlin {
      target "**/*.kt"
      ktlint(versions.ktlint).userData(['indent_size': '2', 'continuation_indent_size': '2'])
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }

  test {
  }

  dokka {
    configuration {
      reportUndocumented = false
      skipDeprecated = true
      jdkVersion = 8
      perPackageOption {
        prefix = "app.cash.certifikit.internal"
        suppress = true
      }
    }
  }
}

subprojects { project ->
  apply plugin: 'maven-publish'
  apply plugin: 'signing'

  publishing {
    publications {
      maven(MavenPublication) {
        groupId = project.group
        artifactId = project.name
        version = project.version
        from components.java
        pom {
          name = project.name
          description = 'Kotlin Certificate processing'
          url = 'https://cashapp.github.io/certifikit/'
          licenses {
            license {
              name = 'The Apache Software License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
          developers {
            developer {
              name = 'Square, Inc.'
            }
          }
          scm {
            connection = 'scm:git:https://github.com/cashapp/certifikit.git'
            developerConnection = 'scm:git:ssh://git@github.com/cashapp/certifikit.git'
            url = 'https://github.com/cashapp/certifikit'
          }
        }
      }
    }

    repositories {
      maven {
        name = "mavencentral"
        url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        credentials {
          username System.getenv('SONATYPE_NEXUS_USERNAME')
          password System.getenv('SONATYPE_NEXUS_PASSWORD')
        }
      }
    }
  }

  signing {
    sign publishing.publications.maven
  }
}

tasks.wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

