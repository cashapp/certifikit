plugins {
    id 'org.jetbrains.kotlin.multiplatform'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

jar {
    manifest {
        attributes('Automatic-Module-Name': 'certifikit')
    }
}

kotlin {
    jvm {
        withJava()
    }

    sourceSets {
        commonMain {
            kotlin.srcDir("$buildDir/generated/sources/kotlinTemplates")
            
            dependencies {
                implementation Dependencies.okio
                implementation("org.jetbrains.kotlinx:kotlinx-datetime:0.3.1")
            }
        }

        commonTest {
            dependencies {
                dependsOn(commonMain)
                implementation(kotlin("test"))
                implementation(Dependencies.okio)
            }
        }

        getByName("jvmMain") {
            dependencies {
                implementation Dependencies.okio
            }
        }

        getByName("jvmTest") {
            dependencies {
                dependsOn(jvmMain)
                implementation(kotlin("test"))
                implementation(Dependencies.okio)
                implementation Dependencies.assertj
            }
        }
    }
}

afterEvaluate { project ->
    project.tasks.dokka {
        outputDirectory = "$rootDir/docs/0.x"
        outputFormat = 'gfm'
    }
}

task copyKotlinTemplates(type: Copy) {
    from "$projectDir/src/commonMain/kotlinTemplates"
    into "$buildDir/generated/sources/kotlinTemplates"
    expand('projectVersion': "${project.version}")
    filteringCharset = 'UTF-8'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    dependsOn(copyKotlinTemplates)
}

