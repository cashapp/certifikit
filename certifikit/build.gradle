plugins {
    id 'org.jetbrains.kotlin.multiplatform'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

jar {
    manifest {
        attributes('Automatic-Module-Name': 'certifikit')
    }
}

kotlin {
    jvm {
        withJava()
    }

    sourceSets {
        commonMain {
            kotlin.srcDir("$buildDir/generated/sources/kotlinTemplates")
            
            dependencies {
                implementation libs.okio
                implementation(libs.org.jetbrains.kotlinx.kotlinx.datetime)
            }
        }

        commonTest {
            dependencies {
                dependsOn(commonMain)
                implementation(kotlin("test"))
                implementation(libs.okio)
                implementation(libs.com.willowtreeapps.assertk)
            }
        }

        getByName("jvmMain") {
            dependencies {
                implementation libs.okio
            }
        }

        getByName("jvmTest") {
            dependencies {
                dependsOn(jvmMain)
                implementation(kotlin("test"))
                implementation(libs.okio)
                implementation libs.assertj
            }
        }
    }
}

afterEvaluate { project ->
    project.tasks.dokka {
        outputDirectory = "$rootDir/docs/0.x"
        outputFormat = 'gfm'
    }
}

task copyKotlinTemplates(type: Copy) {
    from "$projectDir/src/commonMain/kotlinTemplates"
    into "$buildDir/generated/sources/kotlinTemplates"
    expand('projectVersion': "${project.version}")
    filteringCharset = 'UTF-8'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    dependsOn(copyKotlinTemplates)
}

