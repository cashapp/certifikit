import org.apache.tools.ant.taskdefs.condition.Os

plugins {
  id 'application'
  id 'com.palantir.graal' version '0.7.1'
  id "org.jetbrains.kotlin.kapt"
}

application {
  mainClassName = 'app.cash.certifikit.cli.Main'
}

jar {
  manifest {
    attributes 'Automatic-Module-Name': 'app.cash.certifikit.cli'
    attributes 'Main-Class': application.mainClassName
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  kotlinOptions {
    jvmTarget = "1.8"
    allWarningsAsErrors = false
    freeCompilerArgs = ["-Xjsr305=strict", "-Xjvm-default=enable", "-Xopt-in=kotlin.RequiresOptIn"]
  }
}

graal {
  mainClass(application.mainClassName)
  outputName("cft")
  graalVersion('20.2.0')
  javaVersion('11')

  option("--enable-https")
  option("--no-fallback")

  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    // May be possible without, but autodetection is problematic on Windows 10
    // see https://github.com/palantir/gradle-graal
    // see https://www.graalvm.org/docs/reference-manual/native-image/#prerequisites
    windowsVsVarsPath('C:\\Program Files (x86)\\Microsoft Visual Studio\\2019\\BuildTools\\VC\\Auxiliary\\Build\\vcvars64.bat')
  }
}

dependencies {
  api project(':certifikit')
  api dep.okio
  implementation 'info.picocli:picocli:4.5.0'
  implementation 'com.squareup.okhttp3:okhttp:4.9.0'
  implementation 'com.squareup.okhttp3:okhttp-tls:4.9.0'

  implementation("org.jetbrains.kotlin:kotlin-reflect:1.4.10")
  implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.10")
  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9")
  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.3.9")

  kapt "info.picocli:picocli-codegen:4.5.0"
  compileOnly("org.graalvm.nativeimage:svm:20.2.0") {
    // https://youtrack.jetbrains.com/issue/KT-29513
    exclude group: "org.graalvm.nativeimage"
    exclude group: "org.graalvm.truffle"
    exclude group: "org.graalvm.sdk"
    exclude group: "org.graalvm.compiler"
  }

  testImplementation dep.junit
  testImplementation dep.assertj
}
