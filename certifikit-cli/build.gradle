import org.apache.tools.ant.taskdefs.condition.Os

plugins {
  id 'application'
  id 'com.palantir.graal' version '0.7.2'
  id "org.jetbrains.kotlin.kapt"
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

application {
  mainClassName = 'app.cash.certifikit.cli.Main'
}

jar {
  manifest {
    attributes 'Automatic-Module-Name': 'app.cash.certifikit.cli'
    attributes 'Main-Class': application.mainClassName
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  kotlinOptions {
    jvmTarget = "1.8"
    allWarningsAsErrors = false
    freeCompilerArgs = ["-Xjsr305=strict", "-Xjvm-default=enable", "-Xopt-in=kotlin.RequiresOptIn"]
  }
}

graal {
  mainClass(application.mainClassName)
  outputName("cft")
  graalVersion('21.0.0')
  javaVersion('11')

  option("--enable-https")
  option("--no-fallback")
  option("--allow-incomplete-classpath")
  option("--initialize-at-run-time=io.netty.channel")

  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    // May be possible without, but autodetection is problematic on Windows 10
    // see https://github.com/palantir/gradle-graal
    // see https://www.graalvm.org/docs/reference-manual/native-image/#prerequisites
    windowsVsVarsPath('C:\\Program Files (x86)\\Microsoft Visual Studio\\2019\\BuildTools\\VC\\Auxiliary\\Build\\vcvars64.bat')
  }
}

dependencies {
  api project(':certifikit')
  api dep.okio
  implementation('info.picocli:picocli:4.6.1')
  implementation(platform("com.squareup.okhttp3:okhttp-bom:4.10.0-RC1"))
  implementation('com.squareup.okhttp3:okhttp')
  implementation('com.squareup.okhttp3:okhttp-tls')
  implementation('com.squareup.okhttp3:logging-interceptor')

  implementation(platform("io.netty:netty-bom:4.1.58.Final"))
  implementation("io.projectreactor.netty:reactor-netty-core:1.0.3")
  implementation("io.r2dbc:r2dbc-postgresql:0.8.6.RELEASE") {
    exclude group: 'io.projectreactor.netty'
  }
  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-reactive:1.4.2")

  implementation("org.jetbrains.kotlinx:kotlinx-datetime:0.1.1")
  implementation("org.jetbrains.kotlin:kotlin-reflect:1.4.21")
  implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.21")
  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1")
  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.4.1")

  implementation("org.bouncycastle:bcprov-jdk15on:1.68")
  implementation("org.bouncycastle:bcpkix-jdk15on:1.68")
  implementation("org.bouncycastle:bctls-jdk15on:1.68")

  kapt("info.picocli:picocli-codegen:4.6.1")

  implementation('com.squareup.okhttp3:mockwebserver:4.10.0-RC1')
  testImplementation dep.junit5Api
  testImplementation dep.junit5JupiterEngine
  testImplementation dep.assertj
}
