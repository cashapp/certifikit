import org.apache.tools.ant.taskdefs.condition.Os

plugins {
  id 'application'
  id 'com.github.johnrengelman.shadow' version '6.0.0'
  id 'com.palantir.graal' version '0.7.1'
}

application {
  mainClassName = 'app.cash.certifikit.cli.Main'
}

shadowJar {
  mainClassName = application.mainClassName
}

jar {
  manifest {
    attributes 'Automatic-Module-Name': 'app.cash.certifikit.cli'
    attributes 'Main-Class': application.mainClassName
  }
}


//https://github.com/remkop/picocli/tree/master/picocli-codegen
configurations {
  generateConfig
}

task(generateGraalReflectionConfig, dependsOn: 'classes', group:'graal', type: JavaExec) {
  def reflectionOutputFile = "${buildDir}/resources/main/META-INF/native-image/${project.group}/${project.name}/reflect-config.json"

  description 'Generates a temporary graal reflecftion config for native image.'
  outputs.file(reflectionOutputFile)

  main = 'picocli.codegen.aot.graalvm.ReflectionConfigGenerator'
  classpath = configurations.generateConfig + sourceSets.main.runtimeClasspath
  args = ["--output=$reflectionOutputFile", application.mainClassName]
}

graal {
  mainClass(application.mainClassName)
  outputName("cft")
  graalVersion('20.1.0')
  javaVersion('11')

  option("--enable-https")
  option("--no-fallback")

  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    // May be possible without, but autodetection is problematic on Windows 10
    // see https://github.com/palantir/gradle-graal
    // see https://www.graalvm.org/docs/reference-manual/native-image/#prerequisites
    windowsVsVarsPath('C:\\Program Files (x86)\\Microsoft Visual Studio\\2019\\BuildTools\\VC\\Auxiliary\\Build\\vcvars64.bat')
  }
}
nativeImage.dependsOn generateGraalReflectionConfig

dependencies {
  api project(':certifikit')
  api dep.okio
  implementation 'info.picocli:picocli:4.4.0'
  implementation 'com.squareup.okhttp3:okhttp:4.8.0'
  implementation 'com.squareup.okhttp3:okhttp-tls:4.8.0'

  annotationProcessor 'info.picocli:picocli-codegen:4.4.0'
  generateConfig 'info.picocli:picocli-codegen:4.4.0'

  compileOnly("org.graalvm.nativeimage:svm:${graal.graalVersion.get()}")

  testImplementation dep.junit
  testImplementation dep.assertj
}
